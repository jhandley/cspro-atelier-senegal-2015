{Application 'ENQUETE_MENAGE' logic file generated by CSPro}
PROC GLOBAL

PROC ENQUETE_MENAGE_FF

PROC ENQUETE_MENAGE_QUEST

PROC JOUR_INTERVIEW
preproc
DATE_INTERVIEW = sysdate("DDMMYYYY");

PROC HEURE_INTERVIEW
preproc
HEURE_INTERVIEW = systime();
PROC NOMBRE_DE_PIECES_DORMIR

// Contrôler nombre de pieces pour dormir et nombre de pieces
if NOMBRE_DE_PIECES_DORMIR > NOMBRE_DE_PIECES then
	errmsg("Le nombre de pièces pour dormir, %d, dépasse le nombre de pièces total de %d", 
		   NOMBRE_DE_PIECES_DORMIR, NOMBRE_DE_PIECES)
		   select("Corriger nombre de pièces pour dormir", NOMBRE_DE_PIECES_DORMIR, 
		   		  "Corriger nombre de pièces total", NOMBRE_DE_PIECES);
endif
PROC STATUT_OCCUPATION

// Sauter loyer si ce n'est pas en location
if not STATUT_OCCUPATION in 2:3 then
	skip to POPULATION000;
endif
PROC MONTANT_DU_LOYER

PROC POPULATION000

numeric nombrePersonnes = totocc(POPULATION000);

if nombrePersonnes > 0 then

	// Assurer qu'il y a un chef de ménage et pas plus
	// pour les maisons habitées

	// Compter le nombre de chefs (indivus avec LIEN_DE_PARENTE = 1)
	numeric nombreChefs = count(POPULATION000 where LIEN_DE_PARENTE = 1);

	if nombreChefs = 0 then
		errmsg("Le chef de ménage n'a pas été saisie. Il faut un chef pour chaque ménage.");
		reenter LIEN_DE_PARENTE(1);
	elseif nombreChefs > 1 then
		errmsg("Le ménage a %d chefs. Il faut un seul chef pour chaque ménage.", nombreChefs);
		reenter LIEN_DE_PARENTE(1);
	endif;

	// Contrôller l'âge du chef avec l'âge de ses enfants
	
	// Trouver l'index du chef 
	numeric indexChef = seek(LIEN_DE_PARENTE = 1);
	
	// Comparer l'age de chaque enfant avec l'âge du chef
	numeric i;
	do i = 1 while i <= nombrePersonnes 
		if LIEN_DE_PARENTE(i) = 3 and AGE(indexChef) < AGE(i) then
			errmsg("L'âge de l'enfant %s est %d mais l'âge du chef de ménage %s est %d. Le chef doit être plus âgé qui son enfant.",
					strip(NOM_ET_PRENOM(i)), AGE(i), strip(NOM_ET_PRENOM(indexChef)), AGE(indexChef))
					select("Corriger l'âge de " + strip(NOM_ET_PRENOM(i)), AGE(i),
						   "Corriger l'âge de " + strip(NOM_ET_PRENOM(indexChef)), AGE(indexChef),
						   "Corriger le lien de parenté de " + strip(NOM_ET_PRENOM(i)), LIEN_DE_PARENTE(i),
						   "Corriger le lien de parenté de " + strip(NOM_ET_PRENOM(indexChef)), LIEN_DE_PARENTE(indexChef));
		endif;
	enddo;
	
endif;

	
PROC NOM_ET_PRENOM

postproc
// Terminer la saisie si le nom est blanc
if NOM_ET_PRENOM = "" then
	if accept("Terminez saisie du ménage?","Oui", "Non") = 1 then
		endgroup;
	else
		reenter;
	endif;
endif;

PROC LIEN_DE_PARENTE

// Contrôller le lien de parenté avec l'âge
if LIEN_DE_PARENTE = 1 and AGE < 12 then
	errmsg("Le chef du ménage a %d ans mais l'âge minimum est 12", AGE)
		select("Corriger age", AGE, "Corriger lien de parenté", LIEN_DE_PARENTE);
endif;

// Sauter état matrimonial et nombres d'enfants nés vivants pour les
// individus de moins de 12 ans
if AGE < 12 then
	skip to next;
endif
